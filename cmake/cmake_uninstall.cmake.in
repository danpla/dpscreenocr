# Based on:
# https://gitlab.kitware.com/cmake/community/wikis/FAQ#can-i-do-make-uninstall-with-cmake

if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
    message(
        FATAL_ERROR
        "@CMAKE_BINARY_DIR@/install_manifest.txt does not exist")
endif()

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" FILES)
string(REPLACE "\n" ";" FILES "${FILES}")

foreach(FILE ${FILES})
    set(FILE_PATH "$ENV{DESTDIR}${FILE}")

    if(NOT IS_SYMLINK "${FILE_PATH}" AND NOT EXISTS "${FILE_PATH}")
        message(STATUS "Does not exist: ${FILE_PATH}")
        continue()
    endif()

    message(STATUS "Uninstalling: ${FILE_PATH}")

    execute_process(
        COMMAND "@CMAKE_COMMAND@" -E remove "${FILE_PATH}"
        RESULT_VARIABLE RM_RESULT)

    if(NOT RM_RESULT STREQUAL 0)
        message(FATAL_ERROR "Can't remove ${FILE_PATH}")
    endif()
endforeach()
