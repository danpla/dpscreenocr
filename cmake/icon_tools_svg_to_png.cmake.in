# The script requires 3 arguments: SVG_PATH, PNG_PATH, and SIZE.

set(RSVG_CONVERT_EXE "@RSVG_CONVERT_EXE@")
set(OPTIPNG_EXE "@OPTIPNG_EXE@")

get_filename_component(PNG_DIR "${PNG_PATH}" DIRECTORY)
file(MAKE_DIRECTORY "${PNG_DIR}")

execute_process(
    COMMAND
        "${RSVG_CONVERT_EXE}"
        "${SVG_PATH}"
        "--height=${SIZE}"
        "--width=${SIZE}"
        --keep-aspect-ratio
        --format=png
        "--output=${PNG_PATH}"
    RESULT_VARIABLE EXIT_STATUS)
if(NOT EXIT_STATUS EQUAL 0)
    message(FATAL_ERROR "rsvg-convert failed")
endif()

execute_process(
    COMMAND
        "${OPTIPNG_EXE}"
        -quiet
        "${PNG_PATH}"
    RESULT_VARIABLE EXIT_STATUS)
if(NOT EXIT_STATUS EQUAL 0)
    message(FATAL_ERROR "optipng failed")
endif()
