
set(APPIMAGE_BUILD_SCRIPT "${CMAKE_BINARY_DIR}/build_appimage.cmake")

configure_file(
    "${CMAKE_CURRENT_LIST_DIR}/dist_unix_appimage_script.cmake.in"
    "${APPIMAGE_BUILD_SCRIPT}"
    @ONLY)

# Although this is the default name generated by linuxdeploy, we
# still specify it explicitly to avoid making any assumptions.
set(APPIMAGE_NAME
    "${APP_NAME}-${APP_VERSION}-${CMAKE_SYSTEM_PROCESSOR}.AppImage")
set(APPIMAGE_PATH "${CMAKE_BINARY_DIR}/${APPIMAGE_NAME}")

add_custom_command(
    OUTPUT "${APPIMAGE_PATH}"
    COMMAND
        ${CMAKE_COMMAND}
        -D APPIMAGE_PATH=${APPIMAGE_PATH}
        -P ${APPIMAGE_BUILD_SCRIPT}
    DEPENDS "${APPIMAGE_BUILD_SCRIPT}"
    VERBATIM)

add_custom_target(appimage DEPENDS "${APPIMAGE_PATH}")
