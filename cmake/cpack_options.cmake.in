
# Per-generator overrides.

if(CPACK_GENERATOR STREQUAL "DEB")
    set(CPACK_STRIP_FILES ON)
    set(CPACK_PACKAGING_INSTALL_PREFIX "/usr")

    # CMake 3.6 and newer has CPACK_DEBIAN_FILE_NAME variable that can
    # be set to DEB-DEFAULT. We still need to support 2.8.12, so
    # compose the name manually.
    execute_process(
        COMMAND dpkg --print-architecture
        OUTPUT_VARIABLE CPACK_DEBIAN_PACKAGE_ARCHITECTURE
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    set(
        CPACK_PACKAGE_FILE_NAME
        "${CPACK_DEBIAN_PACKAGE_NAME}_${CPACK_PACKAGE_VERSION}_${CPACK_DEBIAN_PACKAGE_ARCHITECTURE}"
    )
elseif(CPACK_GENERATOR MATCHES "NSIS")
    # We set MUI_STARTMENUPAGE_DEFAULTFOLDER with CPACK_NSIS_DEFINES,
    # because otherwise the Start menu page defaults to
    # CPACK_NSIS_PACKAGE_NAME that contains the program version.
    #
    # However, CPACK_NSIS_DEFINES is ignored during component-based
    # installation due to the CMake bug #14522, so we disable
    # component install till the bug is fixed. We use
    # CPACK_MONOLITHIC_INSTALL because
    # CPACK_<GENERATOR>_COMPONENT_INSTALL has no effect in case of
    # NSIS.
    set(CPACK_MONOLITHIC_INSTALL ON)
endif()
