
set(ENABLE_NLS @DPSO_ENABLE_NLS@)

if(CMAKE_VERSION VERSION_LESS 3.15)
    # cmake --install was added in 3.15. Of course, we can make a
    # fallback to "cmake --build . --target install" or "make install
    # DESTDIR=", but it doesn't make much sense to bother since
    # automatic detection of DLLs (GET_RUNTIME_DEPENDENCIES) is only
    # available in >= 3.16.
    message(
        FATAL_ERROR
        "You need CMake >= 3.15 to prepare the NSIS installer"
    )
endif()

function(make_clean_install install_dir)
    execute_process(
        COMMAND @CMAKE_COMMAND@ --build "@CMAKE_BINARY_DIR@"
    )
    execute_process(
        COMMAND @CMAKE_COMMAND@ --install "@CMAKE_BINARY_DIR@" --prefix "${install_dir}"
        # There's also CMAKE_INSTALL_MESSAGE since CMake 3.1.
        OUTPUT_QUIET
    )
endfunction()

# Generate nsis_install/uninstall_list.nsh files based on contents of
# install_dir.
function(gen_file_lists install_dir)
    file(
        GLOB_RECURSE
        DIST_DATA
        LIST_DIRECTORIES true
        RELATIVE "${install_dir}"
        "${install_dir}/*.*"
    )

    list(SORT DIST_DATA)
    # Reverse so that RMDir is called for empty directories.
    list(REVERSE DIST_DATA)

    string(REPLACE "/" "\\" install_dir_WIN "${install_dir}")
    file(
        WRITE
        "@CMAKE_BINARY_DIR@/nsis_install_list.nsh"
        "  SetOutPath \"$INSTDIR\"\n  File /r \"${install_dir_WIN}\\*.*\"\n"
    )

    set(UNINSTALL_LIST_NSH "@CMAKE_BINARY_DIR@/nsis_uninstall_list.nsh")
    file(REMOVE "${UNINSTALL_LIST_NSH}")

    foreach(N ${DIST_DATA})
        if(IS_DIRECTORY "${install_dir}/${N}")
            set(NSIS_COMMAND "RMDir")
        else()
            set(NSIS_COMMAND "Delete")
        endif()

        string(REPLACE "/" "\\" N_WIN "${N}")
        file(
            APPEND
            "${UNINSTALL_LIST_NSH}"
            "  ${NSIS_COMMAND} \"$INSTDIR\\${N_WIN}\"\n"
        )
    endforeach()
endfunction()

# Generate nsis_language_list.nsh.
function(gen_language_list)
    # This is the mapping from a locale name in po/LINGUAS to a name
    # if the NSIS language file from the "Contrib/Language files" NSIS
    # directory. English is always included and is not listed here.
    # To skip a locale that has no corresponding NSIS language, use -
    # as the language name, e.g.:
    #
    #   set(LANG_NAME_en_GB "-")
    #
    # When adding new entries here, you may find useful the MakeLangId
    # tool shipped with NSIS ("Bin" directory), which prints both
    # locale name and Windows' language ID (LCID) of a language. Each
    # NSIS' language file (.nlf/.nsh) has a language ID on the top.
    set(LANG_NAME_bg "Bulgarian")
    set(LANG_NAME_ca "Catalan")
    set(LANG_NAME_de "German")
    set(LANG_NAME_es "Spanish")
    set(LANG_NAME_fr "French")
    set(LANG_NAME_hr "Croatian")
    set(LANG_NAME_nb_NO "Norwegian")
    set(LANG_NAME_pl "Polish")
    set(LANG_NAME_ru "Russian")
    set(LANG_NAME_tr "Turkish")
    set(LANG_NAME_uk "Ukrainian")
    set(LANG_NAME_zh_CN "SimpChinese")

    set(LANGUAGE_LIST_NSH "@CMAKE_BINARY_DIR@/nsis_language_list.nsh")

    if(NOT ENABLE_NLS)
        file(
            WRITE
            "${LANGUAGE_LIST_NSH}"
            "; No languages since NLS was disabled"
        )
        return()
    endif()

    file(REMOVE "${LANGUAGE_LIST_NSH}")

    set(UNDEFINED_NAMES)

    file(
        STRINGS "@CMAKE_SOURCE_DIR@/po/LINGUAS" LANGS REGEX "^[^#].*"
    )
    foreach(LANG ${LANGS})
        if(NOT LANG_NAME_${LANG})
            list(APPEND UNDEFINED_NAMES "${LANG}")
        elseif(NOT LANG_NAME_${LANG} STREQUAL "-")
            file(
                APPEND
                "${LANGUAGE_LIST_NSH}"
                "!insertmacro MUI_LANGUAGE \"${LANG_NAME_${LANG}}\"\n"
            )
        endif()
    endforeach()

    if(UNDEFINED_NAMES)
        string(
            REPLACE ";" ", " UNDEFINED_NAMES_STR "${UNDEFINED_NAMES}"
        )
        message(
            WARNING
            "Names for the following languages are not defined: ${UNDEFINED_NAMES_STR}. Add them to the list above."
        )
    endif()
endfunction()

set(INSTALL_DIR "@CMAKE_BINARY_DIR@/nsis_installer_files")

make_clean_install("${INSTALL_DIR}")
gen_file_lists("${INSTALL_DIR}")
gen_language_list()
